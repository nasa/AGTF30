%% File : Ambient_TMATS.tlc
%%
%% Description: 
%%   Simulink Coder TLC Code Generation file for Ambient_TMATS
%%
%implements Ambient_TMATS "C"

%% Function: BlockTypeSetup ===============================================
%%
%function BlockTypeSetup(block, system) void
    %%
    %% The Target Language must be C
    %if ::GenCPP==1
    %<LibReportFatalError("This S-Function generated by the Legacy Code Tool must be only used with the C Target Language")>
    %endif

    %<LibAddToCommonIncludes("types_TMATS.h")>

    %<SLibAddToStaticSources("Ambient_TMATS_body.c")>

    %openfile forwardDecls
      extern void Ambient_TMATS_body(double *y, const double *u, const AmbientStruct* prm);
      AmbientStruct ambientStruct;
    %closefile forwardDecls

    %assign srcFile = LibGetModelDotCFile()
    %<LibSetSourceFileSection(srcFile, "Definitions", forwardDecls)>

%endfunction

%% Function: BlockInstanceSetup ===========================================
%%
%function BlockInstanceSetup(block, system) void
    %%
    %<LibBlockSetIsExpressionCompliant(block)>
%endfunction

%% Function: Outputs ======================================================
%%
%function Outputs(block, system) Output
    %%
    ambientStruct.AFARc               = %<LibBlockParameter(AFARc,"","",0)>;
    ambientStruct.X_A_AltVec          = (double*)(%<LibBlockParameterBaseAddr(X_A_AltVec)>);
    ambientStruct.T_A_TsVec           = (double*)(%<LibBlockParameterBaseAddr(T_A_TsVec)>);
    ambientStruct.T_A_PsVec           = (double*)(%<LibBlockParameterBaseAddr(T_A_PsVec)>);
    ambientStruct.X_A_FARVec          = (double*)(%<LibBlockParameterBaseAddr(X_A_FARVec)>);
    ambientStruct.T_A_RtArray         = (double*)(%<LibBlockParameterBaseAddr(T_A_RtArray)>);
    ambientStruct.Y_A_TVec            = (double*)(%<LibBlockParameterBaseAddr(Y_A_TVec)>);
    ambientStruct.T_A_gammaArray      = (double*)(%<LibBlockParameterBaseAddr(T_A_gammaArray)>);
    ambientStruct.A                   = %<LibBlockParameterSize(X_A_AltVec)[1]>;
    ambientStruct.B                   = %<LibBlockParameterSize(X_A_FARVec)[1]>;
    ambientStruct.C                   = %<LibBlockParameterSize(Y_A_TVec)[1]>;

    ambientStruct.IWork               = &%<LibBlockIWork(Errors,"","",0)>;
    ambientStruct.BlkNm               = "%<LibGetFormattedBlockPath(block)>";

    %assign y_ptr = LibBlockOutputSignalAddr(0, "", "", 0)
    %assign y_dataType = LibBlockOutputSignalDataTypeName(0,"")

    %assign u_ptr = LibBlockInputSignalAddr(0, "", "", 0)
    %assign u_dataType = LibBlockInputSignalDataTypeName(0,"")

    %%
    Ambient_TMATS_body((%<y_dataType>*)%<y_ptr>,\ 
                       (%<u_dataType>*)%<u_ptr>,\ 
                        &ambientStruct);
    %%
%endfunction

%% [EOF]
