%% File : Valve_TMATS.tlc
%%
%% Description: 
%%   Simulink Coder TLC Code Generation file for Valve_TMATS
%%
%implements Valve_TMATS "C"

%% Function: BlockTypeSetup ===============================================
%%
%function BlockTypeSetup(block, system) void
    %%
    %% The Target Language must be C
    %if ::GenCPP==1
    %<LibReportFatalError("This S-Function generated by the Legacy Code Tool must be only used with the C Target Language")>
    %endif

    %<LibAddToCommonIncludes("types_TMATS.h")>

    %<SLibAddToStaticSources("Valve_TMATS_body.c")>

    %openfile forwardDecls
      extern void Valve_TMATS_body(double* y, const double* u, const ValveStruct* prm);
      ValveStruct valveStruct;
    %closefile forwardDecls

    %assign srcFile = LibGetModelDotCFile()
    %<LibSetSourceFileSection(srcFile, "Definitions", forwardDecls)>

%endfunction

%% Function: BlockInstanceSetup ===========================================
%%
%function BlockInstanceSetup(block, system) void
    %%
    %<LibBlockSetIsExpressionCompliant(block)>
%endfunction

%% Function: Outputs ======================================================
%%
%function Outputs(block, system) Output
    %%
    valveStruct.VlvfullyOpen        = %<LibBlockParameter(VlvfullyOpen,"","",0)>;
    valveStruct.VlvdeadZone         = %<LibBlockParameter(VlvdeadZone,"","",0)>;
    valveStruct.Valve_Ae            = %<LibBlockParameter(Valve_Ae,"","",0)>;
    valveStruct.X_V_PRVec           = (double*)(%<LibBlockParameterBaseAddr(X_V_PRVec)>);
    valveStruct.T_V_WcVec           = (double*)(%<LibBlockParameterBaseAddr(T_V_WcVec)>);
    valveStruct.A                   = %<LibBlockParameterSize(X_V_PRVec)[1]>;

    valveStruct.IWork               = &%<LibBlockIWork(Errors,"","",0)>;
    valveStruct.BlkNm               = "%<LibGetFormattedBlockPath(block)>";

    %assign y_ptr = LibBlockOutputSignalAddr(0, "", "", 0)
    %assign y_dataType = LibBlockOutputSignalDataTypeName(0,"")

    %assign u_ptr = LibBlockInputSignalAddr(0, "", "", 0)
    %assign u_dataType = LibBlockInputSignalDataTypeName(0,"")

    %%
    Valve_TMATS_body((%<y_dataType>*)%<y_ptr>,\ 
                         (%<u_dataType>*)%<u_ptr>,\ 
                          &valveStruct);
    %%
%endfunction

%% [EOF]
